import com.android.build.gradle.api.ApplicationVariant

apply plugin: 'com.android.application'
apply plugin: 'com.github.gfx.ribbonizer'

android {
    compileSdkVersion 25
    buildToolsVersion '25'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {}
        beta {
            debuggable true
        }
        canary {
            debuggable false
        }
        release {}
    }

    productFlavors {
        local {}
        staging {}
        production {}
    }
}

ribbonizer {
    iconNames "@drawable/abc_btn_check_to_on_mtrl_000", "@drawable/abc_btn_radio_to_on_mtrl_000"

    builder { ApplicationVariant variant, File iconFile ->
        if (variant.flavorName == "local") {
            return customColorRibbonNameFilter(variant, iconFile, "TEST", "#0000FF")
            //return greenRibbonFilter(variant, iconFile)
        } else if (variant.buildType.name == "debug") {
            return customColorRibbonFilter(variant, iconFile, "#0000FF")
        } else {
            return greenRibbonFilter(variant, iconFile)
        }
    }

    forcedVariantsNames "localCanary"
}

dependencies {
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
}
